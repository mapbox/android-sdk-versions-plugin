apply plugin: 'maven-publish'
apply plugin: 'com.mapbox.sdkRegistry'
apply from: file('./gradle/artifact-settings.gradle')
apply plugin: 'java'

version = project.ext.versionName
group = project.ext.mapboxArtifactGroupId

java {
    withJavadocJar()
    withSourcesJar()
}

task groovyDocs(type: Groovydoc) {
    def sources = project.sourceSets.main.allGroovy
    source = sources
    classpath = project.sourceSets.main.output + project.sourceSets.main.compileClasspath
}

task groovyDocsJar(type: Jar, dependsOn: groovyDocs) {
    archiveClassifier = 'groovydoc'
    from groovyDocs.destinationDir
}

afterEvaluate { project ->
    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifactId = project.ext.mapboxArtifactId
                pom {
                    name = POM_NAME
                    packaging = POM_PACKAGING
                    description = POM_DESCRIPTION
                    url = POM_URL
                    groupId = this.group
                    version = this.version

                    licenses {
                        license {
                            name = POM_LICENCE_NAME
                            url = POM_LICENCE_URL
                            distribution = POM_LICENCE_DIST
                        }
                    }
                    developers {
                        developer {
                            id = POM_DEVELOPER_ID
                            name = POM_DEVELOPER_NAME
                        }
                    }
                    scm {
                        url = POM_SCM_URL
                        connection = POM_SCM_CONNECTION
                        developerConnection = POM_SCM_DEV_CONNECTION
                    }
                }
            }
        }
    }
}

registry {
    sdkName = project.ext.mapboxRegistrySdkName
    production = true
    snapshot = project.ext.versionName.endsWith("-SNAPSHOT")
    override = snapshot
    dryRun = false
    publish = true
    publications = ['mavenJava']
}